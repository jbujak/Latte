-- programs ------------------------------------------------

entrypoints Program ;

Prog.   Program ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

ClassDef.  TopDef ::= "class" Ident "{" [Field] "}" ;

separator nonempty TopDef "" ;

Ar. 	   Arg ::= Type Ident;

separator  Arg "," ;

Field.     Field ::= Type Ident ;

terminator  Field ";" ;

-- statements ----------------------------------------------

Blk.       Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= LVal "=" Expr  ";" ;

Incr.      Stmt ::= LVal "++"  ";" ;

Decr.      Stmt ::= LVal "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

For.       Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

Arr.       Type ::= Type "[]" ;

Class.     Type ::= Ident ;

separator  Type "," ;

-- Type checker types --------------------------------------

TcNone.      TcType ::= "" ;

internal TcInt.       TcType ::= "int" ;

internal TcStr.       TcType ::= "string" ;

internal TcBool.      TcType ::= "boolean" ;

internal TcVoid.      TcType ::= "void" ;

internal TcFun.       TcType ::= TcType "(" [TcType] ")" ;

internal TcArr.       TcType ::= TcType "[]" ;

internal TcClass.     TcType ::= "class" String ;

separator  TcType "," ;

-- Expressions ---------------------------------------------

ELVal.     Expr6 ::= LVal TcType;

EObjNew.   Expr6 ::= "new" Type TcType;

EArrNew.   Expr6 ::= "new" Type "[" Expr "]" TcType ;

ELitNull.  Expr6 ::= "(" Type ")null" TcType ;

ELitInt.   Expr6 ::= Integer TcType ;

ELitTrue.  Expr6 ::= "true" TcType ;

ELitFalse. Expr6 ::= "false" TcType ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" TcType ;

EString.   Expr6 ::= String TcType ;

Neg.       Expr5 ::= "-" Expr6 TcType ;

Not.       Expr5 ::= "!" Expr6 TcType ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 TcType ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 TcType ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 TcType ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 TcType ;

EOr.       Expr ::= Expr1 "||" Expr TcType ;

coercions  Expr 6 ;

separator  Expr "," ;

-- L-values

ObjField.  LVal ::= LVal "." Ident TcType ;

ArrElem.   LVal ::= LVal "[" Expr "]" TcType ;

Var.       LVal ::= Ident TcType ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

